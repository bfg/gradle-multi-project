//
// build.gradle
//

buildscript {
    ext {
        dependencyMananagementVer = "0.5.6.RELEASE"
        springBootVersion = '1.3.2.RELEASE'
        springCloudVersion = '1.0.3.RELEASE'
        springTestVer = '4.2.5.RELEASE'

        springFoxVer = "2.4.0"
        swaggerVer = "1.5.6"

        slf4jVer = "1.7.15"
        jacksonVer = "2.6.5"
        lombokVer = "1.16.8"
        spockVer = "1.0-groovy-2.4"

        releaseVer = "2.3.5"
        groovyVer = "2.4.6"
        assertjVer = "3.4.1"
    }

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven { url 'http://maven.springframework.org/release' }
        maven { url 'http://repo.spring.io/plugins-release' }
    }

    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "net.researchgate:gradle-release:${releaseVer}"
        classpath "io.spring.gradle:dependency-management-plugin:${dependencyMananagementVer}"
        classpath "org.springframework.boot:spring-boot-gradle-plugin"
    }
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'eclipse'

task wrapper(type: Wrapper) {
    gradleVersion = '2.12'
    distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-all.zip"
}

allprojects {
    group 'com.github.bfg.multi-project'
    version '1.0.0-SNAPSHOT'

    apply plugin: 'java'
    apply plugin: 'groovy'
    apply plugin: 'idea'
    apply plugin: 'eclipse'

    apply plugin: 'io.spring.dependency-management'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }

    // compile groovy sources with invokedynamic support
    tasks.withType(GroovyCompile) {
        groovyOptions.optimizationOptions.indy = true
    }

    dependencyManagement {
        dependencies {
            // groovy: invoke dynamic version
            dependency "org.codehaus.groovy:groovy-all:${groovyVer}:indy"

            // lombok: groovy-like conciseness in pure java at compile time
            dependency "org.projectlombok:lombok:${lombokVer}"

            // spring stuff
            dependency "org.springframework.boot:spring-boot-starter-web:${springBootVersion}"
            dependency "org.springframework.boot:spring-boot-starter-undertow:${springBootVersion}"
            dependency "org.springframework.boot:spring-boot-starter-actuator:${springBootVersion}"
            dependency "org.springframework.boot:spring-boot-starter-log4j2:${springBootVersion}"

            // springfox/swagger
            dependency "io.swagger:swagger-annotations:${swaggerVer}"
            dependency "io.springfox:springfox-swagger2:${springFoxVer}"
            dependency "io.springfox:springfox-swagger-ui:${springFoxVer}"

            // testing
            dependency "org.spockframework:spock-core:${spockVer}"
            dependency "org.spockframework:spock-spring:${spockVer}"
            dependency "org.springframework.boot:spring-boot-starter-test:${springBootVersion}"
            dependency "org.assertj:assertj-core:${assertjVer}"
        }
    }
}

subprojects {
    dependencies {
        testCompile "org.codehaus.groovy:groovy-all",
                "org.spockframework:spock-core",
                "org.spockframework:spock-spring",
                "org.assertj:assertj-core",
                "org.springframework.boot:spring-boot-starter-test"
    }
}